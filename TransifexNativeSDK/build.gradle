// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.versions = [
            'androidXAnnotation' : '1.7.0',
            'appcompat' : '1.6.1',
            'material' : '1.9.0',
            'truth' : '1.1.3',
            'gson' : '2.9.1',
            'junit' : '4.13.2',
            'androidXTestCore' : '1.5.0',
            'roboelectric' : '4.10.3',
            'mockitoCore' : '5.5.0',
            'okhttp3Mockwebserver' : '4.10.0',
            'androidXJunit' : '1.1.5',
            'androidxEspressoCore' : '3.5.1'
    ]
    ext {
        sdkVersionCode = 7                      // version code for txsdk
        sdkVersion = '1.2.1'                    // version for txsdk and common
        pomGroupID = "com.transifex.txnative"   // pom group id for txsdk and common

        cliVersion = '1.2.1'                    // clitool version
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
    id 'com.android.application' version '8.1.0' apply false
    id 'com.android.library' version '8.1.0' apply false
}

// Initialize publishing/signing extra properties with environmental vars
ext['signing.keyId'] = System.getenv('SIGNING_KEY_ID') ?: ''
ext['signing.password'] = System.getenv('SIGNING_PASSWORD') ?: ''
ext['signing.secretKeyRingFile'] = System.getenv('SIGNING_SECRET_KEY_RING_FILE') ?: ''
ext['ossrhUsername'] = System.getenv('OSSRH_USERNAME') ?: ''
ext['ossrhPassword'] = System.getenv('OSSRH_PASSWORD') ?: ''
// Override with local.properties if available
File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
}

// If the key content is in an environmental var, write it to "tmp/key.pgp" and update
// ext['signing.secretKeyRingFile'] to point to it
def pgpKeyContent = System.getenv('PGP_KEY_CONTENTS')
if (pgpKeyContent != null) {
    def tmpDir = new File("$rootProject.rootDir/tmp")
    mkdir tmpDir
    def keyFile = new File("$tmpDir/key.pgp")
    keyFile.createNewFile()
    def os = keyFile.newDataOutputStream()
    os.write(pgpKeyContent.decodeBase64())
    os.close()
    pgpKeyContent = ''

    ext['signing.secretKeyRingFile'] = keyFile.absolutePath
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = 'e1e4b9ea52730'
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = ossrhUsername
            password = ossrhPassword
            version = sdkVersion
        }
    }
}

gradle.projectsEvaluated {

    task aggregatedJavadoc(type: Javadoc, description: 'Generate javadocs from "common" and "txsdk" modules',
            group: JavaBasePlugin.DOCUMENTATION_GROUP) {
        options.encoding 'utf-8'
        options {
            addStringOption 'docencoding', 'utf-8'
            addStringOption 'charset', 'utf-8'
            addStringOption 'source', '8'
            addBooleanOption('Xdoclint:none', true)
            addStringOption 'overview', 'doc/readme.html'
            links 'https://d.android.com/reference'
            linksOffline 'https://d.android.com/reference/', 'https://d.android.com/reference/androidx/'
        }
        title = "Transifex Native SDK $sdkVersion"

        destinationDir project.file("$project.buildDir/docs/javadoc")

        Set<Project> projectSet = subprojects.findAll{ subproject ->
            subproject.name == 'txsdk' || subproject.name == 'common'
        }

        // Merge the properties of the androidJavadoc tasks of the subprojects
        source = projectSet.androidJavadoc.source
        classpath = project.files(projectSet.androidJavadoc.classpath)
        excludes = projectSet.androidJavadoc.excludes.flatten().unique()
        includes = projectSet.androidJavadoc.includes.flatten().unique()
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
    delete "$rootProject.rootDir/tmp"
}

tasks.register('cleanTmp', Delete) {
    delete "$rootProject.rootDir/tmp"
}