plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace = 'com.transifex.txnative'

    defaultConfig {
        minSdkVersion 18
        compileSdk 33
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

//        gradle.startParameter.taskNames.each {
//            if (it.contains("AndroidTest")) {
//                multiDexEnabled true
//            }
//        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    // https://developer.android.com/studio/publish-library/configure-pub-variants
    publishing {
        singleVariant('release') {
            // Currently, Javadoc generation from AGP does not have the same quality as the one
            // generated by the script at publish-helper.gradle so it's commented out.
//            withJavadocJar()
            withSourcesJar()
        }
    }
}

tasks.withType(Test).configureEach {
    systemProperty "file.encoding", "UTF-8"
}

configurations.configureEach {
    resolutionStrategy {
        force "androidx.annotation:annotation:$versions.androidXAnnotation"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    compileOnly "androidx.annotation:annotation:$versions.androidXAnnotation"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    compileOnly "com.google.android.material:material:$versions.material"
    implementation("dev.b3nedikt.viewpump:viewpump:4.0.14") {
        exclude(group : "androidx.appcompat", module :  "appcompat")
    }

    api project(':common')

    testCompileOnly "androidx.annotation:annotation:$versions.androidXAnnotation"
    testImplementation "junit:junit:$versions.junit"
    testImplementation "com.google.truth:truth:$versions.truth"
    testImplementation "androidx.test:core:$versions.androidXTestCore"
    testImplementation "org.robolectric:robolectric:$versions.roboelectric"
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.okhttp3Mockwebserver"
    testImplementation "org.mockito:mockito-core:$versions.mockitoCore"
    testImplementation testFixtures(project(':common'))

    androidTestCompileOnly "androidx.annotation:annotation:$versions.androidXAnnotation"
    androidTestImplementation "androidx.test.ext:junit:$versions.androidXJunit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.androidxEspressoCore"
    androidTestImplementation "com.google.truth:truth:$versions.truth"
    androidTestImplementation testFixtures(project(':common'))
}

apply from: '../publish-helper.gradle'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Specify custom artifactId if needed,
                // otherwise it would use module's name by default.
                artifactId = "txsdk"
            }
        }
    }
}